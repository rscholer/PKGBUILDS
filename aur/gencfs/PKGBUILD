# Maintainer: Raphael Scholer <rscholer@gmx.de>
# Contributor: Andre Klitzing <andre () incubo () de>
pkgname=gencfs
pkgver=1.1
pkgrel=9
pkgdesc="EncFS GUI for encrypted directory mounting"
arch=("i686" "x86_64")
url="http://sallu.tuxfamily.org"
license=("custom")
depends=("encfs" "gtk2")
makedepends=("cmake")
source=("${url}/IMG/bz2/${pkgname}-${pkgver}.tar.bz2"
        "license.txt")
sha256sums=('cd2a5901e942a61972e602cabc092ca5577cd9f2e4f7668398e6d2ef5432182e'
            '2dcf0966f7eaae3af05f3e640215ac537c76d938d50ad02ad9d4260f1ca4bda5')

build() {
  cmake -D CMAKE_INSTALL_PREFIX=/usr "${pkgname}-${pkgver}"
  make
}

package() {
  make DESTDIR="${pkgdir}" install

  # install license
  install -m 644 -D "license.txt" \
    "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"

  # doc folder is not placed correctly,
  # also there is nothing of interest in it
  rm -r "${pkgdir}/usr/doc"
}

# vim:set ts=2 sw=2 et:
