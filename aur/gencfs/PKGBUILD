# Maintainer: Raphael Scholer <rscholer@gmx.de>
# Contributor: Andre Klitzing <andre () incubo () de>
pkgname=gencfs
pkgver=1.1
pkgrel=6
pkgdesc="EncFS GUI for encrypted directory mounting"
arch=("i686" "x86_64")
url="http://sallu.tuxfamily.org"
license=("custom")
depends=("encfs" "gtk2")
makedepends=("cmake")
source=("http://sallu.tuxfamily.org/IMG/bz2/${pkgname}-${pkgver}.tar.bz2"
        "license.txt")
md5sums=('1cedb4f74808394ca0488006fda52995'
         '2d740db2d9a00ce8e4d54d3ea8789b5a')

build() {
  cmake -D CMAKE_INSTALL_PREFIX=/usr "${pkgname}-${pkgver}"
  make
}

package() {
  make DESTDIR="${pkgdir}" install

  # install license
  install -m 644 -D "license.txt" \
    "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"

  # doc folder is not placed correctly,
  # also there is nothing of interest in it
  rm -r "${pkgdir}/usr/doc"
}

# vim:set ts=2 sw=2 et:
