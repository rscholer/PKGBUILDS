# Maintainer: Raphael Scholer <rscholer@gmx.de>
pkgname=xfce4-kbdleds-plugin-git
pkgver=0.0.6_5_g83e40e0
pkgrel=2
pkgdesc="Xfce keyboard LEDs panel plugin"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-kbdleds-plugin"
license=('GPL2')
depends=('xfce4-panel' 'libxfce4ui')
makedepends=('git' 'intltool' 'xfce4-dev-tools')
conflicts=('xfce4-kbdleds-plugin')
source=("git+git://git.xfce.org/panel-plugins/xfce4-kbdleds-plugin")
install="${pkgname}.install"
md5sums=('SKIP')

pkgver() {
    cd "xfce4-kbdleds-plugin"
    echo "$(git describe --always |sed 's#xfce4-kbdleds-plugin-##g;s#-#_#g;s#v##')"
}

build() {
  cd "xfce4-kbdleds-plugin"
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "xfce4-kbdleds-plugin"
  make DESTDIR="${pkgdir}" install
}
# vim:set ts=2 sw=2 et:
