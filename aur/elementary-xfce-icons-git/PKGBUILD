# Maintainer: Raphael Scholer <rscholer@gmx.de>
pkgname=elementary-xfce-icons-git
pkgver=0.3_38_ga4ee2bd
pkgrel=1
pkgdesc='Elementary icons with better Xfce support'
arch=('any')
url='https://github.com/shimmerproject/elementary-xfce'
license=('GPL2')
makedepends=('git')
depends=('gnome-icon-theme')
options=('!strip')
conflicts=('elementary-xfce-icons')
install="${pkgname}.install"
source=("git+${url}")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/elementary-xfce"
  echo "$(git describe --always |sed 's#-#_#g;s#v##')"
}

package() {
  mkdir -p "${pkgdir}/usr/share/icons"
  for variant in  elementary-xfce{,-dark{,er,est}}; do
    cd "${srcdir}/elementary-xfce"
    cp -r "${variant}" "${pkgdir}/usr/share/icons"

    # Remove unneeded files
    cd "${pkgdir}/usr/share/icons"
    rm -r "${variant}"/{AUTHORS,CONTRIBUTORS,LICENSE}
  done
}
# vim:set ts=2 sw=2 et:
