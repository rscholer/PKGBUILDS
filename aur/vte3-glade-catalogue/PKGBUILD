# Maintainer: Raphael Scholer <rascholer@gmail.com>
pkgname=vte3-glade-catalogue
pkgver=0.38.3
pkgrel=1
pkgdesc="Glade catalogue files for vte3"
arch=('i686' 'x86_64')
license=('LGPL')
options=('!emptydirs')
makedepends=('intltool' 'glade')
depends=('vte3')
url="http://www.gnome.org"
source=(http://download.gnome.org/sources/vte/${pkgver::4}/vte-$pkgver.tar.xz)
sha256sums=('3f3ae063207ceea9ca2abd1cf9c9f8bd2b66ca82156609f31f3554288c0c2bf4')

build() {
    cd "vte-$pkgver"
    ./configure --prefix=/usr --sysconfdir=/etc \
        --libexecdir=/usr/lib/vte \
        --localstatedir=/var --disable-static \
        --disable-introspection --disable-gnome-pty-helper \
        --disable-vala --disable-nls\
        --enable-glade-catalogue
    make -C glade
}

package(){
    cd "vte-$pkgver"
    make -C glade DESTDIR="$pkgdir" install

    # Fix naming of icons
    for size in 16 22; do
        cd "${pkgdir}/usr/share/glade/pixmaps/hicolor/${size}x${size}/actions"
        mv widget-vte-terminal.png widget-vte-2.91-terminal.png
    done
}
