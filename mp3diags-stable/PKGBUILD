# Maintainer: Raphael Scholer <rscholer@gmx.de>
# Contributors: Dan Serban, Dany Martineau, Jordi De Groof, RTFreedman

pkgname=mp3diags-stable
pkgver=1.0.11.076
pkgrel=1
pkgdesc="Identify issues with your MP3 files, fix those issues and make other changes"
url="http://mp3diags.sourceforge.net/"
license=("GPL")
arch=("i686" "x86_64")
depends=("boost-libs" "qt")
makedepends=("boost")
optdepends=('mp3gain: MP3 normalization support')
provides=(mp3diags)
conflicts=(mp3diags)
source=("http://downloads.sf.net/mp3diags/mp3diags-src/MP3Diags-${pkgver}.tar.gz")
md5sums=('96503bd5c61970ff8f598b607b101273')

build() {
	cd "${srcdir}/MP3Diags-${pkgver}"
	./AdjustMt.sh
	qmake
	make
}

package() {
	cd "${srcdir}/MP3Diags-${pkgver}"
	mkdir -p "${pkgdir}/usr/bin" "${pkgdir}/usr/share/applications"
	install -p -m755 "bin/MP3Diags" "${pkgdir}/usr/bin/"
	install -p -m644 "desktop/MP3Diags.desktop" "${pkgdir}/usr/share/applications/"


	for i in "16" "22" "24" "32" "36" "40" "48"; do
		mkdir -p "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps"
		install -p -m644 "desktop/MP3Diags${i}.png" "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/MP3Diags.png"
	done
}
# vim: set sw=2 ts=2 et:
