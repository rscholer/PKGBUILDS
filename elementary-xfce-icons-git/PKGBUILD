# Maintainer: Raphael Scholer <rscholer@gmx.de>
pkgname=elementary-xfce-icons-git
pkgver=20120501
pkgrel=1
pkgdesc='Elementary icons with better Xfce support'
arch=('any')
url='https://github.com/shimmerproject/elementary-xfce'
license=('GPL')
makedepends=('git')
depends=("gnome-icon-theme")
conflicts=('elementary-xfce-icons')
options=(!strip)
install='elementary-xfce-icons.install'

_gitroot=https://github.com/shimmerproject/elementary-xfce.git
_gitname=elementary-xfce.git

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}" && git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}" "${_gitname}"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
}

package() {
  cd "${srcdir}/${_gitname}-build"
  mkdir -p "${pkgdir}/usr/share/icons"
  cp -r elementary-xfce{,-dark} "${pkgdir}/usr/share/icons"

  # Remove unneeded files
  cd "${pkgdir}/usr/share/icons"
  rm elementary-xfce{,-dark}/{AUTHORS,CONTRIBUTORS,LICENSE}
}
# vim:set ts=2 sw=2 et:
