# Maintainer: Raphael Scholer <rascholer@gmail.com>

_pkgname='st'
pkgname='st-local'
pkgver=0.7
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url="http://${_pkgname}.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft')
makedepends=('git')
_patchurl="${url}/patches"
source=("http://dl.suckless.org/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        "scrollback.diff::${_patchurl}/${_pkgname}-scrollback-${pkgver}.diff"
        'config.diff')
sha1sums=('358bca5bfc6ee85352dcd4f45f13f78474e5de42'
          'f42d0b0ae25491792acbc6b3ffab025027957a13'
          'ebe82517b240d09d8ce0bb5bd74db6377b7adb87')

provides=("${_pkgname}")
conflicts=("${_pkgname}" "${_pkgname}-git")

prepare() {
  cd "${_pkgname}-${pkgver}"

  # Use Flags as defined in makepkg.conf
  sed \
    -e 's/CFLAGS =/CFLAGS +=/g' \
    -e 's/CPPFLAGS =/CPPFLAGS +=/g' \
    -e 's/LDFLAGS =/LDFLAGS +=/g' \
    -i config.mk

  # Do not build terminfo descriptions. ncurses distributes them already.
  sed '/@tic/d' -i Makefile

  # Apply patches
  patch -Np1 <"${srcdir}/scrollback.diff"

  # # Apply changes to configuration file
  make config.h
  patch -Np1 <"${srcdir}/config.diff"
}

build() {
  cd "${_pkgname}-${pkgver}"

  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${_pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR="${pkgdir}" install

  install -m 755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}
# vim:set ts=2 sw=2 et:
