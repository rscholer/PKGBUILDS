# Maintainer: Raphael Scholer <rascholer@gmail.com>

_pkgname='st'
pkgname='st-local'
pkgver=0.8
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url="https://${_pkgname}.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft')
makedepends=('git')
source=("http://dl.suckless.org/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        # "${url}/patches/hidecursor/${_pkgname}-hidecursor-${pkgver}.diff"
        # "${url}/patches/scrollback/${_pkgname}-scrollback-${pkgver}.diff"
        "${url}/patches/hidecursor/${_pkgname}-hidecursor-20170404-745c40f.diff"
        "${url}/patches/scrollback/${_pkgname}-scrollback-20180311-c5ba9c0.diff"
        'config.diff')
sha256sums=('77353920d07d66c684a0f57ec37c2670c42fdc5c871d6382b701601cdc597576'
            'bce5188fbd008f4bbf2c46aaddba0b691e56788d5706361f315e4c0f3c35836a'
            'a7f61a8a9e0cdc03dbb8c0e929dcfec939d7d9ce0392db45cb01f1f9f812cc51'
            'd668fe238bb1d3ce2697d01fbe603b28c53df685eb9afe91c12934b1b854b019')

provides=("${_pkgname}")
conflicts=("${_pkgname}" "${_pkgname}-git")

prepare() {
  cd "${_pkgname}-${pkgver}"

  # Use Flags as defined in makepkg.conf
  sed \
    -e 's/CPPFLAGS =/CPPFLAGS +=/g' \
    -i config.mk

  # Do not build terminfo descriptions. ncurses distributes them already.
  sed \
    -e '/tic/d' \
    -e '/@echo Please/d' \
    -i Makefile

  # Apply patches
  patch -Np1 <"${srcdir}/${_pkgname}-hidecursor-20170404-745c40f.diff"
  # patch -Np1 <"${srcdir}/${_pkgname}-hidecursor-${pkgver}.diff"
  patch -Np1 <"${srcdir}/${_pkgname}-scrollback-20180311-c5ba9c0.diff"
  # patch -Np1 <"${srcdir}/${_pkgname}-scrollback-${pkgver}.diff"

  # Apply changes to configuration file
  make config.h
  patch -Np1 <"${srcdir}/config.diff"
}

build() {
  cd "${_pkgname}-${pkgver}"

  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${_pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR="${pkgdir}" install

  install -m 755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}
# vim:set ts=2 sw=2 et:
