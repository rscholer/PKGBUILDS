# Maintainer: Raphael Scholer <rascholer@gmail.com>

_pkgname='st'
pkgname='st-local'
pkgver=0.7
pkgrel=7
pkgdesc='Simple virtual terminal emulator for X'
url="https://${_pkgname}.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft')
makedepends=('git')
source=("http://dl.suckless.org/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        "${url}/patches/hidecursor/${_pkgname}-hidecursor-${pkgver}.diff"
        "${url}/patches/scrollback/${_pkgname}-scrollback-${pkgver}.diff"
        'config.diff')
sha256sums=('f7870d906ccc988926eef2cc98950a99cc78725b685e934c422c03c1234e6000'
            '51290d95297062c9d077d2ffc803e71f6ea95a5ff3ff59182f80ba0327de2ae1'
            'f721b15a5aa8d77a4b6b44713131c5f55e7fca04006bc0a3cb140ed51c14cfb6'
            '5ba937c60182b8af0ca75a6246b2b20d9ea0b84a82fce23a3ce433b50452c94a')

provides=("${_pkgname}")
conflicts=("${_pkgname}" "${_pkgname}-git")

prepare() {
  cd "${_pkgname}-${pkgver}"

  # Use Flags as defined in makepkg.conf
  sed \
    -e 's/CFLAGS =/CFLAGS +=/g' \
    -e 's/CPPFLAGS =/CPPFLAGS +=/g' \
    -e 's/LDFLAGS =/LDFLAGS +=/g' \
    -i config.mk

  # Do not build terminfo descriptions. ncurses distributes them already.
  sed '/@tic/d' -i Makefile

  # Apply patches
  patch -Np1 <"${srcdir}/${_pkgname}-hidecursor-${pkgver}.diff"
  patch -Np1 <"${srcdir}/${_pkgname}-scrollback-${pkgver}.diff"

  # Apply changes to configuration file
  make config.h
  patch -Np1 <"${srcdir}/config.diff"
}

build() {
  cd "${_pkgname}-${pkgver}"

  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${_pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR="${pkgdir}" install

  install -m 755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}
# vim:set ts=2 sw=2 et:
