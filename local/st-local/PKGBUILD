# Maintainer: Raphael Scholer <rascholer@gmail.com>

_pkgname='st'
pkgname='st-local'
pkgver=0.8
pkgrel=3
pkgdesc='Simple virtual terminal emulator for X'
url="https://${_pkgname}.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft')
makedepends=('git')
source=("http://dl.suckless.org/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        "${url}/patches/hidecursor/${_pkgname}-hidecursor-${pkgver}.diff"
        "${url}/patches/scrollback/${_pkgname}-scrollback-${pkgver}.diff"
        'config.diff')
sha256sums=('77353920d07d66c684a0f57ec37c2670c42fdc5c871d6382b701601cdc597576'
            '433cc79cffe3266660924c09be3d8209127065eba1740cb9e22bee54154c6768'
            '8279d347c70bc9b36f450ba15e1fd9ff62eedf49ce9258c35d7f1cfe38cca226'
            'd668fe238bb1d3ce2697d01fbe603b28c53df685eb9afe91c12934b1b854b019')

provides=("${_pkgname}")
conflicts=("${_pkgname}" "${_pkgname}-git")

prepare() {
  cd "${_pkgname}-${pkgver}"

  # Use Flags as defined in makepkg.conf
  sed \
    -e 's/CPPFLAGS =/CPPFLAGS +=/g' \
    -i config.mk

  # Do not build terminfo descriptions. ncurses distributes them already.
  sed \
    -e '/tic/d' \
    -e '/@echo Please/d' \
    -i Makefile

  # Apply patches
  patch -Np1 <"${srcdir}/${_pkgname}-hidecursor-${pkgver}.diff"
  patch -Np1 <"${srcdir}/${_pkgname}-scrollback-${pkgver}.diff"

  # Apply changes to configuration file
  make config.h
  patch -Np1 <"${srcdir}/config.diff"
}

build() {
  cd "${_pkgname}-${pkgver}"

  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${_pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR="${pkgdir}" install

  install -m 755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}
# vim:set ts=2 sw=2 et:
