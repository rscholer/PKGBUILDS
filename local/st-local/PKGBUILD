# Maintainer: Raphael Scholer <rascholer@gmail.com>

_pkgname='st'
pkgname='st-local'
pkgver=0.8.1
pkgrel=7
pkgdesc='Simple virtual terminal emulator for X'
url="https://${_pkgname}.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft')
source=("http://dl.suckless.org/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        "${url}/patches/clipboard/${_pkgname}-clipboard-${pkgver}.diff"
        "${url}/patches/hidecursor/${_pkgname}-hidecursor-${pkgver}.diff"
        "${url}/patches/scrollback/${_pkgname}-scrollback-0.8.diff"
        'config.diff'
        'selclear.diff')
sha256sums=('c4fb0fe2b8d2d3bd5e72763e80a8ae05b7d44dbac8f8e3bb18ef0161c7266926'
            'f22e0165aacb2bc86d000728c81f68022abcc601dbfd09e516e1ba772225d7e6'
            'bf3fe4e855f67fc9ae69b7328399ce06567f6aae3c9fb7fc8e7ec26c89e41dfd'
            '8279d347c70bc9b36f450ba15e1fd9ff62eedf49ce9258c35d7f1cfe38cca226'
            'b25350923b18a0956097f2c1b1ad3d5dae4b12ae1362736eb1674d1ca70a2bd8'
            '763a7795cd094d7840139683373e38d783cb1db597893cf8aeb01b90474b5ebf')

provides=("${_pkgname}")
conflicts=("${_pkgname}" "${_pkgname}-git")

prepare() {
  cd "${_pkgname}-${pkgver}"

  # Use Flags as defined in makepkg.conf
  sed \
    -e 's/CPPFLAGS =/CPPFLAGS +=/g' \
    -i config.mk

  # Do not build terminfo descriptions. ncurses distributes them already.
  sed \
    -e '/tic/d' \
    -e '/@echo Please/d' \
    -i Makefile

  # Apply patches
  patch -Np1 <"${srcdir}/${_pkgname}-clipboard-${pkgver}.diff"
  patch -Np1 <"${srcdir}/${_pkgname}-hidecursor-${pkgver}.diff"
  patch -Np1 <"${srcdir}/${_pkgname}-scrollback-0.8.diff"
  patch -Np1 <"${srcdir}/selclear.diff"

  # Apply changes to configuration file
  make config.h
  patch -Np1 <"${srcdir}/config.diff"
}

build() {
  cd "${_pkgname}-${pkgver}"

  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${_pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR="${pkgdir}" install

  install -m 755 -d "${pkgdir}/usr/share/${_pkgname}"
  install -m 755 -d "${pkgdir}/usr/share/licenses/${pkgname}"

  install -m 644 -t "${pkgdir}/usr/share/${_pkgname}" "${_pkgname}.info"
  install -m 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}
# vim:set ts=2 sw=2 et:
