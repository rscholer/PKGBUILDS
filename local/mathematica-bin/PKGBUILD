# Maintainer: Raphael Scholer <rscholer@gmx.de>
pkgname=mathematica-bin
pkgver=9.0.1
pkgrel=1
pkgdesc="Computational software program for technical computing."
arch=('i686' 'x86_64')
url="http://www.wolfram.com/mathematica/"
license=('custom')
depends=('ttf-bitstream-vera'
         'font-mathematica'
         'xorg-fonts-alias'
         'xorg-fonts-type1')
replaces=("mathematica" "wolfram-mathematica")
conflicts=("mathematica" "wolfram-mathematica")
provides=("mathematica")
source=("Mathematica_${pkgver}_LINUX.sh"
        "license.pdf"
        "wolfram-mathematica.desktop")
install="${pkgname}.install"
options=('libtool' 'staticlibs' '!strip' '!upx')
md5sums=('7fcbc4d1488757b10ef07740ac30a580'
         '237b80869cc89f8cccd327234cbbb2f6'
         '04705bb2989f8a10b2f8e493bdaf2c0a')

# Don't compress pkg tarball
PKGEXT=".pkg.tar"

# To build this package you need to place the mathematica-installer into
# your startdir.

prepare() {
  chmod +x "./Mathematica_${pkgver}_LINUX.sh"
}

package() {
  insdir="/opt/${pkgname}"
  targetdir="${pkgdir}${insdir}"

  # Extract Mathematica
  ./Mathematica_${pkgver}_LINUX.sh -- -execdir="${pkgdir}"/usr/bin \
    -targetdir="${targetdir}" -auto

  # Install license
  install -m 644 -D "license.pdf" \
    "${pkgdir}/usr/share/licenses/${pkgname}/license.pdf"

  # Fixing symbolic links
  for f in $(find "${targetdir}/Executables" -type f ! -name '.*'); do
    f="$(basename "${f}")"

    if [[ -e "${pkgdir}/usr/bin/${f}" ]]; then
      rm "${pkgdir}/usr/bin/${f}"
    fi

    ln -s "${insdir}/Executables/${f}" "${pkgdir}/usr/bin/${f}"
  done

  rm "${pkgdir}/usr/bin/MathematicaScript"
  if [[ "${CARCH}" == "x86_64" ]]; then
    ln -s ${insdir}/SystemFiles/Kernel/Binaries/Linux-x86-64/MathematicaScript \
      "${pkgdir}/usr/bin/"
  else
    ln -s ${insdir}/SystemFiles/Kernel/Binaries/Linux/MathematicaScript \
      "${pkgdir}/usr/bin/"
  fi

  # Copying menu and mimetype information
  mkdir -p "${pkgdir}"/usr/share/applications
  mkdir -p "${pkgdir}"/usr/share/mime/packages

  install -m 664 -D ./wolfram-mathematica.desktop \
    "${targetdir}/SystemFiles/Installation/wolfram-mathematica.desktop"
  install -m 664 -D ./wolfram-mathematica.desktop \
    "${targetdir}/SystemFiles/Installation/wolfram-mathematica${pkgver%%.*}.desktop"
  install -m 664 -D ./wolfram-mathematica.desktop \
    "${targetdir}/SystemFiles/Installation/Mathematica.desktop"

  ln -s "${insdir}/SystemFiles/Installation/wolfram-mathematica.desktop" \
    "${pkgdir}/usr/share/applications/wolfram-mathematica.desktop"

  for f in $(find "${targetdir}/SystemFiles/Installation" -type f -name '*.xml'); do
    f="$(basename "${f}")"
    ln -s "${insdir}/SystemFiles/Installation/${f}" \
      "${pkgdir}/usr/share/mime/packages/"
   done

  # Copying icons
  cd "${targetdir}"/SystemFiles/FrontEnd/SystemResources/X
  for i in "32" "64" "128"; do
    mkdir -p "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps"
    mkdir -p "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/mimetypes"

    # Apps
    for f in $(find "${targetdir}"/SystemFiles/FrontEnd/SystemResources/X/ \
      \( -name "M*-${i}.png" -o -name "vnd.*-${i}.png" \) -not -name '*Doc*.png'); do
      f="$(basename "${f}")"
      g="$(echo ${f/\-${i}/} |tr '[:upper:]' '[:lower:]')"

      ln -s "${insdir}/SystemFiles/FrontEnd/SystemResources/X/${f}" \
        "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/wolfram-${g}"
    done

    # Mimetypes
    for f in $(find "${targetdir}"/SystemFiles/FrontEnd/SystemResources/X/ \
      -name "*Doc-${i}.png"); do
      f="$(basename "${f}")"
      g="$(echo ${f/Doc/} |tr '[:upper:]' '[:lower:]')"
      g="${g/\-${i}/}"

      ln -s "${insdir}/SystemFiles/FrontEnd/SystemResources/X/${f}" \
        "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/mimetypes/application-${g}"
      ln -s "${insdir}/SystemFiles/FrontEnd/SystemResources/X/${f}" \
        "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/mimetypes/gnome-mime-application-${g}"
    done
  done

  # Copying manpages
  for section in "1" "2" "3" "4" "5" "6" "7" "8" "9"; do
    for f in $(find "${targetdir}/SystemFiles/SystemDocumentation/Unix" -type f -name "*.${section}"); do
      f="$(basename "${f}")"
      if [[ "${section}" == "1" && ! -e "${pkgdir}/usr/bin/${f/\.${section}/}" ]]; then
        continue
      else
        mkdir -p "${pkgdir}/usr/share/man/man${section}"
        ln -s "${insdir}/SystemFiles/SystemDocumentation/Unix/${f}" \
          "${pkgdir}/usr/share/man/man${section}/${f}"
      fi
    done
  done
}

# vim:set ts=2 sw=2 et:
