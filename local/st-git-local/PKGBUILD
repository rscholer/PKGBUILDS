# Maintainer: Raphael Scholer <rascholer@gmail.com>
# Contributor: mar77i <mysatyre at gmail dot com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>

pkgname=st-git-local
_pkgname=st
pkgver=0.7.r5.gf739843
pkgrel=2
pkgdesc='Simple virtual terminal emulator for X'
url='http://st.suckless.org'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft')
makedepends=('git')
_patchurl="${url}/patches"
source=('git://git.suckless.org/st'
        "scrollback.diff::${_patchurl}/${_pkgname}-scrollback-20160924-f739843.diff"
        "scrollback-mouse.diff::${_patchurl}/${_pkgname}-scrollback-mouse-20161020-6e79e83.diff"
        "scrollback-mouse-altscreen.diff::${_patchurl}/${_pkgname}-scrollback-mouse-altscreen-20161020-6e79e83.diff"
        'config.diff')
sha1sums=('SKIP'
          '4b155f6fc15ba994d8e13f79f51a6f14e6308996'
          '88b85b0f3dff3606c5c791ab5752fdfb36727c7c'
          'a891faa40d51641dc3f54d472cdcfa8fa83e6fc7'
          'ebe82517b240d09d8ce0bb5bd74db6377b7adb87')

provides=("${_pkgname}" "${pkgname}-git")
conflicts=("${_pkgname}" "${pkgname}-git")

pkgver() {
  cd "${_pkgname}"

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${_pkgname}"

  # Use Flags as defined in makepkg.conf
  sed \
    -e 's/CFLAGS =/CFLAGS +=/g' \
    -e 's/CPPFLAGS =/CPPFLAGS +=/g' \
    -e 's/LDFLAGS =/LDFLAGS +=/g' \
    -i config.mk

  # Do not build terminfo descriptions. ncurses distributes them already.
  sed '/@tic/d' -i Makefile

  # Apply patches
  patch -Np1 <"${srcdir}/scrollback.diff"
  patch -Np1 <"${srcdir}/scrollback-mouse.diff"
  patch -Np1 <"${srcdir}/scrollback-mouse-altscreen.diff"

  # Apply changes to configuration file
  make config.h
  patch -Np1 <"${srcdir}/config.diff"
}

build() {
  cd "${_pkgname}"

  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${_pkgname}"

  make PREFIX=/usr DESTDIR="${pkgdir}" install

  install -m 755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}
# vim:set ts=2 sw=2 et:
