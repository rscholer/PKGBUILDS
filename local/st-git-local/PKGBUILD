# Maintainer: Raphael Scholer <rascholer@gmail.com>
# Contributor: mar77i <mysatyre at gmail dot com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>

pkgname=st-git-local
_pkgname=st
pkgver=0.6.r73.g235b438
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://git.suckless.org/st/'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
source=('git://git.suckless.org/st'
	'config.h'
	'scrollback.diff')
sha1sums=('SKIP'
          '2f7cf3d5c3884aff7a2f71b15ad9e8e11667d64d'
          'dc5cebde897e98e4b2d309a4da4e8db82435a516')

provides=("${_pkgname}" "${pkgname}-git")
conflicts=("${_pkgname}" "${pkgname}-git")

pkgver() {
	cd "${srcdir}/${_pkgname}"

	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "${_pkgname}"

	sed \
		-e 's/CFLAGS =/CFLAGS +=/g' \
		-e 's/CPPFLAGS =/CPPFLAGS +=/g' \
		-e 's/LDFLAGS =/LDFLAGS +=/g' \
		-e 's/_BSD_SOURCE/_DEFAULT_SOURCE/' \
		-i config.mk

	patch -Np1 <"${srcdir}/scrollback.diff"
}

build() {
	cd "${_pkgname}"

	cp "${srcdir}/config.h" .

	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${_pkgname}"

	make PREFIX=/usr DESTDIR="${pkgdir}" install

	install -m 755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}
