# Maintainer: Raphael Scholer <rscholer@gmx.de>
_pkgname=dwt
pkgname=${_pkgname}-git
pkgver=0.5.1_52_g5473ae8
pkgrel=2
pkgdesc='Simple no-frills terminal emulator based on VTE with Tango palette'
arch=('i686' 'x86_64')
url="https://github.com/aperezdc/${_pkgname}"
license=('MIT')
depends=('vte3')
conflicts=("${_pkgname}")
makedepends=('python-docutils')
source=("git+${url}"
		"install_path.patch"
		"resize_hint.patch"
		"tango_palette.patch")
sha1sums=('SKIP'
          'd64576c7f1bbea55ded798874b78a5c0fdb9eb30'
          '708d85adbdc654528f882d52cf479a8564a91f41'
          'd8d6149f89ca75d617fef620ca030ffa4b6ac93b')

pkgver() {
	cd "${_pkgname}"
	echo "$(git describe --tags --always |sed 's#-#_#g;s#v##')"
}

prepare() {
	cd "${_pkgname}"
	patch -Np1 < ../install_path.patch
	patch -Np1 < ../resize_hint.patch
	patch -Np1 < ../tango_palette.patch
}

build() {
	cd "${_pkgname}"
	make PREFIX=/usr
}

package() {
	cd "${_pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}/" install

	install -m 755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m 664 -t "${pkgdir}/usr/share/licenses/${pkgname}" COPYING
}
