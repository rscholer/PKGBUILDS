# Maintainer: Raphael Scholer <rscholer@gmx.de>
pkgname=wolfram-mathematica
pkgver=9.0.1
pkgrel=5
pkgdesc="Computational software program for technical computing."
arch=('i686' 'x86_64')
url="http://www.wolfram.com/mathematica/"
license=('custom')
depends=('ttf-bitstream-vera'
         'font-mathematica'
         'xorg-fonts-alias'
         'xorg-fonts-type1')
provides=("mathematica")
source=("Mathematica_${pkgver}_LINUX.sh"
        "license.pdf")
install="${pkgname}.install"
# Don't strip binaries. This greatly reduces build time by introducing a
# negligible gain in package size (< 1 Mib).
# It will also avoid a lot of errors when running Mathematica.
options=('!strip')
md5sums=('7fcbc4d1488757b10ef07740ac30a580'
         '237b80869cc89f8cccd327234cbbb2f6')

# Don't compress pkg tarball
PKGEXT=".pkg.tar"

# To build this package you need to place the mathematica-installer into
# your startdir.

prepare() {
  cd "${srcdir}"
  chmod +x "./Mathematica_${pkgver}_LINUX.sh"
}

package() {
  cd "${srcdir}"

  # Extract Mathematica
  ./Mathematica_${pkgver}_LINUX.sh -- -execdir="${pkgdir}"/usr/bin \
    -targetdir="${pkgdir}"/opt/Mathematica -auto

  # Install license
  install -m 644 -D "license.pdf" \
    "${pkgdir}/usr/share/licenses/${pkgname}/license.pdf"

  # Fixing symbolic links
  cd "${pkgdir}"/usr/bin
  rm ./*
  ln -s /opt/Mathematica/Executables/math
  ln -s /opt/Mathematica/Executables/mathematica
  ln -s /opt/Mathematica/Executables/Mathematica
  ln -s /opt/Mathematica/Executables/mcc
  ln -s /opt/Mathematica/Executables/MathKernel
  if [[ "${CARCH}" == "x86_64" ]]; then
    ln -s /opt/Mathematica/SystemFiles/Kernel/Binaries/Linux-x86-64/MathematicaScript
  else
    ln -s /opt/Mathematica/SystemFiles/Kernel/Binaries/Linux/MathematicaScript
  fi

  # Copying menu and mimetype information
  mkdir -p "${pkgdir}"/usr/share/applications
  mkdir -p "${pkgdir}"/usr/share/mime/packages

  cd "${pkgdir}"/opt/Mathematica/SystemFiles/Installation

  sed -e 's/^[ \t]*//' -i wolfram-mathematica9.desktop
  sed -e 's/^Exec=.*/Exec=\/opt\/Mathematica\/Executables\/Mathematica -fs/' \
      -i wolfram-mathematica9.desktop
  sed -e 's/^TryExec=.*/TryExec=\/opt\/Mathematica\/Executables\/Mathematica/' \
      -i wolfram-mathematica9.desktop
  echo "Categories=Development;Math;Science;" >> wolfram-mathematica9.desktop
  install -m 664 -D wolfram-mathematica9.desktop \
    "${pkgdir}"/usr/share/applications/wolfram-mathematica.desktop
  install -m 664 -D *.xml "${pkgdir}"/usr/share/mime/packages/


  # Copying icons
  cd "${srcdir}"
  mkdir -p "${pkgdir}"/usr/share/icons/hicolor/{32x32,64x64,128x128}/{apps,mimetypes}
  cd "${pkgdir}"/opt/Mathematica/SystemFiles/FrontEnd/SystemResources/X
  for i in "32" "64" "128"; do
    install -m 664 -D Mathematica-${i}.png \
      "${pkgdir}"/usr/share/icons/hicolor/${i}x${i}/apps/wolfram-mathematica.png
    install -m 664 -D MathematicaPlayer-${i}.png \
      "${pkgdir}"/usr/share/icons/hicolor/${i}x${i}/apps/wolfram-mathematicaplayer.png
    install -m 664 -D vnd.wolfram.cdf-${i}.png \
      "${pkgdir}"/usr/share/icons/hicolor/${i}x${i}/mimetypes/application-vnd.wolfram.cdf.png

    install -m 664 -D MathematicaDoc-${i}.png \
      "${pkgdir}"/usr/share/icons/hicolor/${i}x${i}/mimetypes/application-mathematica.png
    install -m 664 -D MathematicaPlayerDoc-${i}.png \
      "${pkgdir}"/usr/share/icons/hicolor/${i}x${i}/mimetypes/application-mathematicaplayer.png
    install -m 664 -D vnd.wolfram.cdfDoc-${i}.png \
      "${pkgdir}"/usr/share/icons/hicolor/${i}x${i}/mimetypes/application-vnd.wolfram.cdf.png

    install -m 664 -D MathematicaDoc-${i}.png \
      "${pkgdir}"/usr/share/icons/hicolor/${i}x${i}/mimetypes/gnome-mime-application-mathematica.png
    install -m 664 -D MathematicaPlayerDoc-${i}.png \
      "${pkgdir}"/usr/share/icons/hicolor/${i}x${i}/mimetypes/gnome-mime-application-mathematicaplayer.png
    install -m 664 -D vnd.wolfram.cdfDoc-${i}.png \
      "${pkgdir}"/usr/share/icons/hicolor/${i}x${i}/mimetypes/gnome-mime-application-vnd.wolfram.cdf.png
  done

  # Copying manpages
  mkdir -p "${pkgdir}"/usr/share/man/man1
  cd "${pkgdir}"/opt/Mathematica/SystemFiles/SystemDocumentation/Unix
  install -m 664 -D *.1 "${pkgdir}"/usr/share/man/man1
}

# vim:set ts=2 sw=2 et:
