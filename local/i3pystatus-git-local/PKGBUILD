# Maintainer: Raphael Scholer <rascholer@gmail.com>

_pkgname='i3pystatus'
pkgname="${_pkgname}-git-local"
pkgver=3.35.r255.g0f4480e
pkgrel=1
pkgdesc='A complete replacement for i3status'
url="https://github.com/enkore/${_pkgname}"
arch=('any')
license=('MIT')
optdepends=('i3ipc-python'
	'python-basiciw'
	'python-beautifulsoup4'
	'python-colour'
	'python-cssselect'
	'python-dbus'
	'python-lxml'
	'python-netifaces'
	'python-psutil'
	'python-pyalsaaudio'
	'python-pywapi'
	'wireless_tools'
)
makedepends=('git' 'python-setuptools')
source=("git+${url}")
sha256sums=('SKIP')

provides=("${_pkgname}")
conflicts=("${_pkgname}" "${_pkgname}-git")

pkgver() {
	cd "${_pkgname}"

	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "${_pkgname}"

	python setup.py build
}

package() {
	cd "${_pkgname}"

	python setup.py install --skip-build --root="${pkgdir}" --optimize=1

	install -m 755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m 644 -t "${pkgdir}/usr/share/licenses/${PKGNAME}" MIT-LICENSE
}
# vim:set ts=2 sw=2 et:
