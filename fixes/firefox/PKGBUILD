pkgname='firefox'
pkgver=61.0.2
pkgrel=0
pkgdesc="Standalone web browser from mozilla.org"
arch=('i686')
license=(MPL GPL LGPL)
url="https://www.mozilla.org/${pkgname}/"
depends=(
  gtk3
  mozilla-common
  libxt
  startup-notification
  mime-types
  dbus-glib
  ffmpeg
  nss
  hunspell-en_US
  sqlite
  ttf-font
  libpulse
  libvpx
  icu
)
optdepends=(
  'networkmanager: Location detection via available WiFi networks'
  'libnotify: Notification integration'
  'pulseaudio: Audio support'
  'speech-dispatcher: Text-to-Speech'
)
options=(!emptydirs !strip)
_url_release="https://ftp.mozilla.org/pub/${pkgname}/releases"
source=(
  "${_url_release}/${pkgver}/linux-i686/en-US/${pkgname}-${pkgver}.tar.bz2"
  "${pkgname}-symbolic.svg"
  "${pkgname}.desktop"
  'vendor.js'
)
sha256sums=('829c8d38b05212b82e1c340c1467ea3c2690184cd319d682b865b97f8d4b2da6'
            '9a1a572dc88014882d54ba2d3079a1cf5b28fa03c5976ed2cb763c93dabbd797'
            '677e1bde4c6b3cff114345c211805c7c43085038ca0505718a11e96432e9811a'
            '531b9c5c0526d1f97e6ce388c4b95e70045e97008fb17318b00653c99f65bd31')

package() {
  install -m 755 -d "${pkgdir}/usr/lib"
  cp -r "${srcdir}/${pkgname}" "${pkgdir}/usr/lib/${pkgname}"

  # Allow ${pkgname} to be started without manipulating $PATH.
  install -m 755 -d "${pkgdir}/usr/bin"
  ln -s "/usr/lib/${pkgname}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # Install desktop file.
  install -m 755 -d "${pkgdir}/usr/share/applications"
  cp "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications"

  # Install icons.
  for i in 16 32 48 64 128; do
    install -m 755 -d "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps"

    ln -s "/usr/lib/${pkgname}/browser/chrome/icons/default/default${i}.png" \
          "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/${pkgname}.png"
  done

  install -m 755 -d "${pkgdir}/usr/share/icons/hicolor/symbolic/apps"
  cp "${srcdir}/${pkgname}-symbolic.svg" \
     "${pkgdir}/usr/share/icons/hicolor/symbolic/apps/${pkgname}-symbolic.svg"

  # Install archlinux speciific settings.
  install -m 755 -d "${pkgdir}/usr/lib/${pkgname}/browser/defaults/preferences"
  cp "${srcdir}/vendor.js" \
     "${pkgdir}/usr/lib/${pkgname}/browser/defaults/preferences/vendor.js"
}

# vim:set ts=2 sw=2 et:
